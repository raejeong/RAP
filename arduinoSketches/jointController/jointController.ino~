/*
 * Joint Controller
 * Runs on every actuator
 * Rae Jeong :: raychanjeong@gmail.com :: MakeLab
 */

#include <Encoder.h> 
#include <SoftwareSerial.h>
#include <PID_v1.h>

int limitSwitchPin = 7; // Assigning pin 7 for the limit switch. Note that pin 7 has the internal pull up
int encPinA = 2;
int encPinB = 3;
int mySerialPinRX = 4;
int mySerialPinTX = 5;

Encoder myEnc(encPinA, encPinB); // Reading Encoder data from pin 2 and 3. Note that we use 2 and 3 on an UNO because these are the interupt pins 

SoftwareSerial mySerial(mySerialPinRX, mySerialPinTX); // Software Serial for communicating with the Syren 25A motor controller at baud rate of 9600, using pin 4 and 5.

double setPoint;
double input;
double output;

PID myPID(&input, &output, &setPoint, 2, 5, 1, DIRECT);

boolean limitSwitchLastReading; // Last reading of the limit switch 
long limitSwitchLastDebounceTime = 0; // Starting the debounce time at 0
long limitSwitchDebounceDelay = 50; // Setting the debounce delay time in milliseconds
boolean limitSwitchState = true; // State of the limit switch for debouncing
boolean limitSwitchReading; // Current reading of the limit switch
int limitSwitchCount = 0;

/*
 * motorControllerCommand
 * Gobal variable to store the value sent to the motor controller.
 * maxForward: 255 maxReverse: 0 motorStop: 127
 * maxForward, maxReverse and motorStop variables are used in the calibration
 * Direction was chosen based on the encoder direction
 */
int motorControllerCommand = 255;
int maxForward = 255;
int maxReverse = 0;
int motorStop = 127; 


void setup()
{
  Serial.begin(9600); // Serial communication start.
  mySerial.begin(9600); // Serial commuication to motor controller start.

  pinMode(limitSwitchPin, INPUT); 
  
  digitalWrite(limitSwitchPin, HIGH); // Enabling the internal pull up
  
  limitSwitchLastReading = digitalRead(limitSwitchPin); // Reading the limit switch data in

  input = encoderRead();
  setPoint = 0;
  myPID.SetMode(AUTOMATIC);
  myPID.SetOutputLimits(maxReverse-motorStop, maxForward-motorStop);
  
  calibration(); // Running the calibration code on every start up

}

void loop()
{
  input = encoderRead();
  if(Serial.available() > 0) {
    setPoint = Serial.read();
  }
  Serial.print("output: ");
  Serial.print(output+127);
  Serial.print(" input: ");
  Serial.print(input);
  Serial.print(" setPoint: ");
  Serial.println(setPoint, DEC);
  myPID.Compute();
  mySerial.write(output+127);
  delay(1);
}


/*
 * When called, limitSwitch() returns true when the limit switch is pressed 
 * and false when the limit switch is not pressed.
 */
boolean limitSwitch()
{
  limitSwitchReading = digitalRead(limitSwitchPin);
  
  if(limitSwitchLastReading!=limitSwitchReading) {
    limitSwitchLastDebounceTime = millis();
    limitSwitchState = false;
  }
  
  limitSwitchLastReading = limitSwitchReading;
  
  if(!limitSwitchState && (millis() - limitSwitchLastDebounceTime) > limitSwitchDebounceDelay) {
    limitSwitchState = true;
  }
  return !limitSwitchReading;
  
}


/*
 * calibration() fucntion that is called every time the system starts up
 * Is responsible for hitting the limit switch and resetting the encoder to zero
 */
void calibration()
{
  // If the motor starts on the limitSwitch, go reverse until the limit switch is no longer triggered.
  if(limitSwitch()) { 
    while(limitSwitch()) {
      mySerial.write(maxReverse);
    }
  }
  
  // Go forward until the motor triggers the limit switch.
  while(!limitSwitch()) {
    mySerial.write(maxForward);
  }
  
  // Stop the motor
  mySerial.write(motorStop);

  // Reset the encoder. The delay is there becasue the motor does not stop instantaneously.
  delay(500);
  myEnc.write(0);
}


/*
 * mapFloat is meant to execute mapping for floats
 */
float mapFloat(float x, float in_min, float in_max, float out_min, float out_max)
{
  return (x - in_min) * (out_max - out_min) / (in_max - in_min) + out_min;
}


/*
 * encoderRead() function is used to read the encoder value in degrees
 */
float encoderRead() {
  return mapFloat(myEnc.read(), 0, 5260, 0, 360);
}


